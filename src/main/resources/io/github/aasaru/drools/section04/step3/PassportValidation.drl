package io.github.aasaru.drools.section04.step3
 
import io.github.aasaru.drools.domain.NewPassport;
import io.github.aasaru.drools.domain.Validation;

rule "Expired passport"
	dialect "java"
	when
		$passport: NewPassport( isExpired()  )
	then
		System.out.println( $passport + " is INVALID because it is expired" );
		modify($passport) {
		  setValidation( Validation.FAILED ),
		  setCause( "passport is expired" )
		}
end

rule "Full passport"
	dialect "java"
	when
		$passport: NewPassport( unusedVisaPages == 0   )
	then
		System.out.println( $passport + " is INVALID because it has no space for visa" );
		modify($passport) {
		  setValidation( Validation.FAILED ),
		  setCause( "passport has no space for visa" )
		}
end

rule "Valid passport"
	dialect "java"
	when
		$passport: NewPassport( validation == Validation.UNKNOWN )
	then
		System.out.println( $passport + " is without validation info, consider CORRECT for now" );
		modify($passport) {
		  setValidation( Validation.PASSED )
		}
end
